
FUNCTION(KOKKOS_DEVICE_OPTION SUFFIX DEFAULT DOCSTRING)
  KOKKOS_OPTION(ENABLE_${SUFFIX} ${DEFAULT} BOOL ${DOCSTRING})
  STRING(TOUPPER ${SUFFIX} UC_NAME)
  IF (KOKKOS_ENABLE_${UC_NAME})
    LIST(APPEND KOKKOS_ENABLED_DEVICES    ${SUFFIX})
    #I hate that CMake makes me do this
    SET(KOKKOS_ENABLED_DEVICES    ${KOKKOS_ENABLED_DEVICES}    PARENT_SCOPE)
  ENDIF()
  SET(KOKKOS_ENABLE_${UC_NAME} ${KOKKOS_ENABLE_${UC_NAME}} PARENT_SCOPE)
  LIST(APPEND KOKKOS_VALID_DEVICES ${UC_NAME})
  SET(KOKKOS_VALID_DEVICES ${KOKKOS_VALID_DEVICES} PARENT_SCOPE)
ENDFUNCTION()

KOKKOS_CFG_DEPENDS(DEVICES NONE)

KOKKOS_DEVICE_OPTION(SERIAL        ON  "Whether to build serial  backend")
KOKKOS_DEVICE_OPTION(PTHREAD       OFF "Whether to build Pthread backend")
KOKKOS_DEVICE_OPTION(ROCM          OFF "Whether to build AMD ROCm backend")

IF(Trilinos_ENABLE_Kokkos AND Trilinos_ENABLE_OpenMP)
  SET(OMP_DEFAULT ON)
ELSE()
  SET(OMP_DEFAULT OFF)
ENDIF()
KOKKOS_DEVICE_OPTION(OPENMP ${OMP_DEFAULT} "Whether to build OpenMP backend")

IF(Trilinos_ENABLE_Kokkos AND TPL_ENABLE_CUDA)
  SET(CUDA_DEFAULT ON)
ELSE()
  SET(CUDA_DEFAULT OFF)
ENDIF()
KOKKOS_DEVICE_OPTION(CUDA ${CUDA_DEFAULT} "Whether to build CUDA backend")

KOKKOS_OPTION(DEVICES "SERIAL" STRING "A list of devices to enable")
IF (KOKKOS_DEVICES MATCHES ",")
  MESSAGE(WARNING "-- Detected a comma in: Kokkos_DEVICES=`${KOKKOS_DEVICES}`")
  MESSAGE("-- Although we prefer KOKKOS_DEVICES to be semicolon-delimited, we do allow")
  MESSAGE("-- comma-delimited values for compatibility with scripts (see github.com/trilinos/Trilinos/issues/2330)")
  STRING(REPLACE "," ";" KOKKOS_DEVICES "${KOKKOS_DEVICES}")
  MESSAGE("-- Commas were changed to semicolons, now Kokkos_DEVICES=`${KOKKOS_DEVICES}`")
ENDIF()

FOREACH(DEV ${KOKKOS_DEVICES})
  IF(NOT DEV IN_LIST KOKKOS_VALID_DEVICES)
      MESSAGE(SEND_ERROR "'${DEV}' entry in Kokkos_DEVICES is not a valid device")
  ENDIF()
  MESSAGE(STATUS "Setting Kokkos_ENABLE_${DEV}=ON from Kokkos_DEVICES")
  GET_PROPERTY(DEV_HELPSTRING CACHE Kokkos_ENABLE_${DEV} PROPERTY HELPSTRING)
  SET(Kokkos_ENABLE_${DEV} ON CACHE BOOL ${DEV_HELPSTRING})
ENDFOREACH()

IF (KOKKOS_ENABLE_CUDA)
  GLOBAL_SET(KOKKOS_DONT_ALLOW_EXTENSIONS "CUDA enabled")
ENDIF()
